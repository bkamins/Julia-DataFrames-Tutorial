# # Introducci贸n a DataFrames
# **[Bogumi Kamiski](http://bogumilkaminski.pl/about/), Apr 21, 2018**
# (Traducci贸n por Miguel Raz Guzm谩n Macedo, 18 de abril de 2021)

using DataFrames
using BenchmarkTools

# ## Tips de Performance 

#-

# ### Accesar columnas por n煤mero es m谩s r谩pido que por nombre

x = DataFrame(rand(5, 1000))
@btime x[500];
@btime x[:x500];

# ### Cuando trabajes con datos de `DataFrame`, usa funciones barrera o anotaciones de tipo
# Sobre funciones barrera: https://docs.julialang.org/en/v1/manual/performance-tips/
# Las anotaciones de tipo se ven as铆: `x::Int64 = 2`

function f_bad() # esta funci贸n va a ser lenta 
    srand(1); x = DataFrame(rand(1000000,2))
    y, z = x[1], x[2]
    p = 0.0
    for i in 1:nrow(x)
        p += y[i]*z[i]
    end
    p
end

@btime f_bad();

#-

@code_warntype f_bad() # la raz贸n es que Julia no conoce los tipos de las columnas del `DataFrame`

#-

## la primera opci贸n es funciones barrera (deber铆a ser posible en casi cualquier c贸digo)
function f_inner(y,z)
   p = 0.0
   for i in 1:length(y)
       p += y[i]*z[i]
   end
   p
end

function f_barrier() # extraemos el trabajo a una funci贸n interior
    srand(1); x = DataFrame(rand(1000000,2))
    f_inner(x[1], x[2])
end

function f_inbuilt() # o usamos una funci贸n preestablecida si es posible
    srand(1); x = DataFrame(rand(1000000,2))
    dot(x[1], x[2])
end

@btime f_barrier();
@btime f_inbuilt();

#-

## la opci贸n 2 es proveer el tipo de las columnas extra铆das - lo cual
## es m谩s sencillo pero hay casos donde no se va a poder saber sus tipos
function f_typed()
    srand(1); x = DataFrame(rand(1000000,2))
    y::Vector{Float64}, z::Vector{Float64} = x[1], x[2]
    p = 0.0
    for i in 1:nrow(x)
        p += y[i]*z[i]
    end
    p
end

@btime f_typed();

# ### Considera usar la t茅cnica de creaci贸n de `DataFrame` demorada

function f1()
    x = DataFrame(Float64, 10^4, 100) # Trabajamos con un DataFrame directamente
    for c in 1:ncol(x)
        d = x[c]
        for r in 1:nrow(x)
            d[r] = rand()
        end
    end
    x
end

function f2()
    x = Vector{Any}(100)
    for c in 1:length(x)
        d = Vector{Float64}(10^4)
        for r in 1:length(d)
            d[r] = rand()
        end
        x[c] = d
    end
    DataFrame(x) # y demoramos al creaci贸n del DataFrame hasta que hayamos acabado nuestro trabaj
end

@btime f1();
@btime f2();

# ### Puedes agregar filas a un `DataFrame` in situ r谩pidamente  

x = DataFrame(rand(10^6, 5))
y = DataFrame(transpose(1.0:5.0))
z = [1.0:5.0;]

@btime vcat($x, $y); # crear un nuevo DataFrame - lento 
@btime append!($x, $y); # in situ - r谩pido    

x = DataFrame(rand(10^6, 5)) # reseteamos al mismo punto inicial
@btime push!($x, $z); # agregar una sola fila - lo m谩s r谩pido

# ### Permitir datos `missing` y `categorical` alenta el c贸mputo

using StatsBase

function test(data) # usa la funci贸n countmap para medir performance
    println(eltype(data))
    x = rand(data, 10^6)
    y = categorical(x)
    println(" raw:")
    @btime countmap($x)
    println(" categorical:")
    @btime countmap($y)
    nothing
end

test(1:10)
test([randstring() for i in 1:10])
test(allowmissing(1:10))
test(allowmissing([randstring() for i in 1:10]))


